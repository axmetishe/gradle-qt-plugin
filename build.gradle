import java.text.SimpleDateFormat

plugins {
  id 'groovy'
  id 'groovy-gradle-plugin'
  id 'maven-publish'
  id 'com.gradle.plugin-publish' version '0.12.0'
  id 'com.github.hierynomus.license' version '0.15.0'
  id "org.sonarqube" version "3.1.1"
  id 'org.ajoberstar.grgit' version '4.1.0'
}

repositories {
  mavenCentral()
  jcenter()
}

wrapper {
  distributionType = Wrapper.DistributionType.ALL
  gradleVersion = '6.7.1'
}

group = 'org.platops.gradle.plugins.qt'
version = '1.0.1'
description = 'Gradle QT plugin for framework specific tasks integration'

String vcsBaseUrl = "https://github.com/axmetishe"
String vcsRepoUrl = "${vcsBaseUrl}/${project.name}.git"

ext {
  javaVersion = 11
  groovyVersion = '3.0.7'
  manifestAttributes = [
    'SCM-URL': vcsRepoUrl,
    'SCM-Branch': grgit.branch.current().getName(),
    'SCM-Revision' : grgit.head().abbreviatedId,
    'Build-Version': project.version,
    'Build-Timestamp': new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
    'Built-By': "Gradle ${gradle.gradleVersion}",
    'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
    'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}",
  ]
}

license {
  ignoreFailures = true
  header = file('LICENSE.txt')

  includes(['**/*.groovy'])
}

sonarqube {
  properties {
    property "sonar.projectKey", "axmetishe_gradle-qt-plugin"
  }
}

gradlePlugin {
  plugins {
    qtPlugin {
      id = 'org.platops.gradle.plugins.qt.gradle-qt-plugin'
      displayName = 'Gradle QT Plugin'
      implementationClass = 'org.platops.gradle.plugins.qt.QTPlugin'
    }
  }
}

publishing {
  repositories {
    mavenLocal()
  }

  publications {
    mavenJava(MavenPublication) {
      jar {
        manifest {
          attributes(project.ext.manifestAttributes)
        }
      }
      pom {
        name = project.name
        description = project.description
        url = "${vcsBaseUrl}/${project.name}"

        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id = 'axmetishe'
            name = 'Evgeny Akhmetkhanov'
            email = 'axmetishe+gradle-qt-plugin@gmail.com'
          }
        }
        scm {
          url = vcsRepoUrl
        }
      }
    }
  }
}

pluginBundle {
  description = project.description
  tags = ['qt', 'qrc', 'moc', 'uic', 'rcc', 'gradle-native', 'c', 'cpp']
  website = vcsBaseUrl
  vcsUrl = vcsRepoUrl

  plugins {
    qtPlugin {
      displayName = 'Gradle QT Plugin'
    }
  }
  manifest {
    attributes(project.ext.manifestAttributes)
  }
}

dependencies {
  api gradleApi()
  implementation "org.codehaus.groovy:groovy:${project.ext.groovyVersion}"
}
