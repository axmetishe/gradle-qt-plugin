import java.text.SimpleDateFormat

plugins {
  id 'groovy'
  id 'groovy-gradle-plugin'
  id 'maven-publish'
  id 'com.gradle.plugin-publish' version '1.2.1'
  id 'com.github.hierynomus.license' version '0.16.1'
  id 'org.sonarqube' version '4.4.1.3373'
  id 'org.ajoberstar.grgit' version '5.0.0-rc.3'
}

repositories {
  mavenCentral()
}

wrapper {
  distributionType = Wrapper.DistributionType.ALL
  gradleVersion = '8.6'
}

group = 'org.platops.gradle.plugins.qt'
version = '1.3.0'
description = 'Gradle QT plugin for framework specific tasks integration'

String vcsBaseUrl = "https://github.com/axmetishe"
String vcsRepoUrl = "${vcsBaseUrl}/${project.name}.git"

license {
  ignoreFailures = true
  header = file('LICENSE.txt')

  includes(['**/*.groovy'])
}

sonarqube {
  properties {
    property "sonar.projectKey", "axmetishe_gradle-qt-plugin"
  }
}

jar {
  manifest {
    attributes([
      'URL': vcsRepoUrl,
      'Branch': grgit.branch.current().getName(),
      'Revision' : grgit.head().abbreviatedId,
    ], "SCM")
    attributes([
      'Version': project.version,
      'Timestamp': new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
      'BuiltBy': "Gradle ${gradle.gradleVersion}",
      'JDK': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
      'OS': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}",
    ], "Build")
  }
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

gradlePlugin {
  website = "${vcsBaseUrl}/${project.name}"
  vcsUrl = vcsRepoUrl
  plugins {
    qtPlugin {
      id = 'org.platops.gradle.plugins.qt.gradle-qt-plugin'
      displayName = 'Gradle QT Plugin'
      implementationClass = 'org.platops.gradle.plugins.qt.QTPlugin'
      description = project.description
      tags.set(['qt', 'qrc', 'moc', 'uic', 'rcc', 'gradle-native', 'c', 'cpp'])
    }
  }
}

publishing {
  repositories {
    mavenLocal()
  }

  publications {
    pluginMaven(MavenPublication) {
      pom {
        name = project.name
        description = project.description
        url = "${vcsBaseUrl}/${project.name}"

        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id = 'axmetishe'
            name = 'Evgeny Akhmetkhanov'
            email = 'axmetishe+gradle-qt-plugin@gmail.com'
          }
        }

        scm {
          url = "${vcsBaseUrl}/${project.name}"
        }
      }
    }
  }
}

dependencies {
  api gradleApi()
}
